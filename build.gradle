plugins {
    id 'org.springframework.boot' version '3.2.5'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'java'
    id 'com.github.node-gradle.node' version '7.0.2'
    id 'com.diffplug.spotless' version('6.25.0')
}

group = 'com.mfscreener.mfapp'
version = '0.0.1-SNAPSHOT'

java.sourceCompatibility = '21'

bootRun {
    environment SPRING_PROFILES_ACTIVE: environment.SPRING_PROFILES_ACTIVE ?: 'local'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-validation')
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')
    implementation('org.liquibase:liquibase-core')
    runtimeOnly('org.postgresql:postgresql')
    implementation('io.github.wimdeblauwe:error-handling-spring-boot-starter:4.2.0')
    implementation('org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0')
    implementation('org.springframework.boot:spring-boot-starter-actuator')
    implementation('org.springframework.boot:spring-boot-starter-cache')
    implementation('org.springframework.boot:spring-boot-starter-data-redis')

    developmentOnly('org.springframework.boot:spring-boot-devtools')
    developmentOnly('org.springframework.boot:spring-boot-docker-compose')

    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('io.rest-assured:rest-assured')
    testAndDevelopmentOnly('org.springframework.boot:spring-boot-testcontainers')
    testAndDevelopmentOnly('org.testcontainers:junit-jupiter')
    testAndDevelopmentOnly('org.testcontainers:postgresql')
}

node {
    download.set(true)
    version.set('20.12.2')
}

task npmRunBuild(type: NpmTask) {
    args = ['run', 'build']
    dependsOn npmInstall

    inputs.files(fileTree('node_modules'))
    inputs.files(fileTree('src/main/webapp'))
    inputs.file('angular.json')
    inputs.file('package.json')
    inputs.file('tsconfig.json')
    inputs.file('tsconfig.app.json')
    outputs.dir(layout.buildDirectory.dir('resources/main/static'))
}

processResources {
    dependsOn npmRunBuild
}

tasks.withType(Test) {
    useJUnitPlatform()
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        palantirJavaFormat("2.42.0")
        formatAnnotations()
    }
}

check.dependsOn spotlessCheck
